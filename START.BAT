@ECHO OFF

CALL PARAMS.BAT
IF EXIST %LOGDIR%\NUL GOTO LOGEXIST
MKDIR %LOGDIR%

:LOGEXIST
ECHO ==========================Ag Group RK6 START Utility============================>> %MAINLOG%
TIMENOW >> %MAINLOG%

ECHO Checking %UNZIPDIR%... >> %MAINLOG%
IF EXIST %UNZIPDIR%\NUL GOTO UZDEXIST
ECHO Creating %UNZIPDIR%... >> %MAINLOG%
MKDIR %UNZIPDIR%

:UZDEXIST
ECHO %UNZIPDIR% EXIST... >> %MAINLOG%
ECHO Update DHCP lease >> %MAINLOG%
DHCP
IF %ERRORLEVEL% == 0 GOTO DHCPOK
ECHO START DHCP: ERROR >> %MAINLOG%
GOTO END

:DHCPOK
ECHO START DHCP: OK >> %MAINLOG%
ECHO Getting %CRC32%.zip from %SERVER%... >> %MAINLOG%
HTGET %SERVER%/%CRC32%.zip > %UNZIPDIR%\%CRC32%.zip
IF %ERRORLEVEL% == 21 GOTO CRCGETOK
ECHO Cannot get %CRC32%.zip from %SERVER%... >> %MAINLOG%
GOTO END

:CRCGETOK
ECHO Getting successfully %CRC32%.zip from %SERVER%... >> %MAINLOG%
ECHO Unzip %CRC32%.zip to %CRCDIR%... >> %MAINLOG%
UNZIP32 -jo %UNZIPDIR%\%CRC32%.zip -d %CRCDIR%
IF %ERRORLEVEL% == 0 GOTO CRCUNZOK
ECHO Cannot unzip %UNZIPDIR%\%CRC32%.zip to %CRCDIR%... >> %MAINLOG%
GOTO END

:CRCUNZOK
ECHO Unzip successfully %CRC32%.zip to %CRCDIR%... >> %MAINLOG%
ECHO Compare %BUNDLEDIR%\%BUNDLE%.zip CRC32... >> %MAINLOG%
TYPE %CRCDIR%\%BUNDLE%.c32 | TR a-f A-F | PIPESET NEWCRC
CRC32 %BUNDLEDIR%\%BUNDLE%.zip | FAM \F1 | TR a-f A-F | PIPESET OLDCRC
IF "%NEWCRC%" == "%OLDCRC%" GOTO BDCRCOK

ECHO Getting %BUNDLE%.zip from %SERVER%... >> %MAINLOG%
HTGET %SERVER%/%BUNDLE%.zip > %BUNDLEDIR%\%BUNDLE%.zip
IF %ERRORLEVEL% == 21 GOTO BDGETOK
ECHO Cannot get %BUNDLE%.zip... >> %MAINLOG%
IF EXIST %BUNDLEDIR%\%BUNDLE%.zip GOTO BDCRCOK
GOTO END

:BDGETOK
ECHO Getting successfully %BUNDLE%.zip from %SERVER%... >> %MAINLOG%
ECHO Compare %BUNDLEDIR%\%BUNDLE%.zip CRC32... >> %MAINLOG%
TYPE %CRCDIR%\%BUNDLE%.c32 | TR a-f A-F | PIPESET NEWCRC
CRC32 %BUNDLEDIR%\%BUNDLE%.zip | FAM \F1 | TR a-f A-F | PIPESET OLDCRC
IF "%NEWCRC%" == "%OLDCRC%" GOTO BDCRCOK

ECHO CRC32 Verification faild: %NEWCRC% ne %OLDCRC% >> %MAINLOG%
GOTO END

:BDCRCOK
ECHO CRC32 Verification successfully: %NEWCRC% eq %OLDCRC% >> %MAINLOG%
ECHO Unzip %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%
UNZIP32 -jo %BUNDLEDIR%\%BUNDLE%.zip -d %UNZIPDIR%
IF %ERRORLEVEL% == 0 GOTO UNZIPOK

ECHO Cannot unzip %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%... >> %MAINLOG%
GOTO END

:UNZIPOK
ECHO Unzip successfully %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%
ECHO Start %GO%... >> %MAINLOG%
CALL %GO%

:END
ECHO Deleting directory %UNZIPDIR% and subdirectories... >> %MAINLOG%
DELDIR %UNZIPDIR%
RD %UNZIPDIR%
TIMENOW >> %MAINLOG%
ECHO ===============================START COMPLETED==================================>> %MAINLOG%
ECHO.