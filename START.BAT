@ECHO OFF

CALL PARAMS.BAT
IF EXIST %LOGDIR%\NUL GOTO LOGEXIST
MKDIR %LOGDIR%

:LOGEXIST
ECHO ==========================Ag Group RK6 START Utility============================>> %MAINLOG%
TIMENOW >> %MAINLOG%

ECHO Checking %UNZIPDIR%... >> %MAINLOG%
IF EXIST %UNZIPDIR%\NUL GOTO UZDEXIST
ECHO Creating %UNZIPDIR%... >> %MAINLOG%
MKDIR %UNZIPDIR%

:UZDEXIST
ECHO %UNZIPDIR% EXIST... >> %MAINLOG%
ECHO Checking %BUNDLEDIR%... >> %MAINLOG%
IF EXIST %BUNDLEDIR%\NUL GOTO BNDLEXST
ECHO Creating %BUNDLEDIR%... >> %MAINLOG%
MKDIR %BUNDLEDIR%

:BNDLEXST
ECHO Update DHCP lease >> %MAINLOG%
DHCP
IF %ERRORLEVEL% == 0 GOTO DHCPOK
ECHO START DHCP: ERROR >> %MAINLOG%
GOTO END

:DHCPOK
ECHO START DHCP: OK >> %MAINLOG%
ECHO Getting %MD5% from %SERVER%... >> %MAINLOG%
HTGET %SERVER%/%MD5% > %UNZIPDIR%\%MD5%
IF %ERRORLEVEL% == 21 GOTO MD5GETOK
ECHO Cannot get %MD5% from %SERVER%... >> %MAINLOG%
GOTO END

:MD5GETOK
ECHO Getting successfully %MD5% from %SERVER%... >> %MAINLOG%
ECHO Convert UNIX2DOS %MD5%... >> %MAINLOG%
UNIX2DOS %UNZIPDIR%\%MD5%
ECHO Compare %BUNDLEDIR%\%BUNDLE%.zip MD5... >> %MAINLOG%
TYPE %UNZIPDIR%\%MD5% | FAM /M1=%BUNDLE%.zip \F0 | TR a-f A-F | PIPESET NEWMD5
MD5 %BUNDLEDIR%\%BUNDLE%.zip | FAM \F3 | TR a-f A-F | PIPESET OLDMD5
IF "%NEWMD5%" == "%OLDMD5%" GOTO BDMD5OK

ECHO Getting %BUNDLE%.zip from %SERVER%... >> %MAINLOG%
HTGET %SERVER%/%BUNDLE%.zip > %BUNDLEDIR%\%BUNDLE%.zip
IF %ERRORLEVEL% == 21 GOTO BDGETOK
ECHO Cannot get %BUNDLE%.zip... >> %MAINLOG%
GOTO END

:BDGETOK
ECHO Getting successfully %BUNDLE%.zip from %SERVER%... >> %MAINLOG%
ECHO Compare %BUNDLEDIR%\%BUNDLE%.zip MD5... >> %MAINLOG%
TYPE %UNZIPDIR%\%MD5% | FAM /M1=%BUNDLE%.zip \F0 | TR a-f A-F | PIPESET NEWMD5
MD5 %BUNDLEDIR%\%BUNDLE%.zip | FAM \F3 | TR a-f A-F | PIPESET OLDMD5
IF "%NEWMD5%" == "%OLDMD5%" GOTO BDMD5OK

ECHO MD5 Verification faild: %NEWMD5% ne %OLDMD5% >> %MAINLOG%
GOTO END

:BDMD5OK
ECHO MD5 Verification successfully: %NEWMD5% eq %OLDMD5% >> %MAINLOG%
ECHO Unzip %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%
UNZIP32 -jo %BUNDLEDIR%\%BUNDLE%.zip -d %UNZIPDIR%
IF %ERRORLEVEL% == 0 GOTO UNZIPOK

ECHO Cannot unzip %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%... >> %MAINLOG%
GOTO END

:UNZIPOK
ECHO Unzip successfully %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%
ECHO Start %GO%... >> %MAINLOG%
CALL %GO%

:END
TIMENOW >> %MAINLOG%
ECHO ===============================START COMPLETED==================================>> %MAINLOG%
ECHO.