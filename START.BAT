@ECHO OFF

CALL PARAMS.BAT
IF EXIST %LOGDIR%\NUL GOTO LOGEXIST
MKDIR %LOGDIR%

:LOGEXIST
ECHO ==========================Ag Group RK6 START Utility========================== >> %MAINLOG%
timenow >> %MAINLOG%

ECHO Checking %UNZIPDIR%... >> %LOGFILE%
IF EXIST %UNZIPDIR%\NUL GOTO UZDEXIST
ECHO Creating %UNZIPDIR%... >> %LOGFILE%
MKDIR %UNZIPDIR%

:UZDEXIST
ECHO Update DHCP lease >> %MAINLOG%
dhcp
IF %ERRORLEVEL% == 0 GOTO DHCPOK
ECHO START DHCP: ERROR >> %MAINLOG%
GOTO END

:DHCPOK
ECHO START DHCP: OK >> %MAINLOG%
ECHO Getting %CRC32% from %SERVER%... >> %MAINLOG%
htget %SERVER%/%CRC32% > %UNZIPDIR%\%CRC32%
IF %ERRORLEVEL% == 21 GOTO CRCGETOK
ECHO Cannot get %CRC32% from %SERVER%... >> %MAINLOG%
GOTO END

:CRCGETOK
ECHO Unzip %CRC32% to %CRCDIR%... >> %MAINLOG%
UNZIP32 -jo %UNZIPDIR%\%CRC32% -d %CRCDIR%
IF %ERRORLEVEL% == 0 GOTO CRCUNZOK
ECHO Cannot unzip %UNZIPDIR%\%CRC32% to %CRCDIR%... >> %MAINLOG%
GOTO END

:CRCUNZOK
ECHO Compare %BUNDLEDIR%\%BUNDLE%.zip CRC32... >> %MAINLOG%
TYPE %CRCDIR%\%BUNDLE%.c32 | TR a-f A-F | PIPESET NEWCRC
CRC32 %BUNDLEDIR%\%BUNDLE%.zip | FAM \F1 | TR a-f A-F | PIPESET OLDCRC
IF "%NEWCRC%" == "%OLDCRC%" GOTO BDCRCOK

ECHO Getting %BUNDLE%.zip from %SERVER%... >> %MAINLOG%
htget %SERVER%/%BUNDLE%.zip > %BUNDLEDIR%\%BUNDLE%.zip
IF %ERRORLEVEL% == 21 GOTO BDGETOK
ECHO Cannot get %BUNDLE%.zip... >> %MAINLOG%
IF EXIST %BUNDLEDIR%\%BUNDLE%.zip GOTO BDCRCOK
GOTO END

:BDGETOK
ECHO Compare %BUNDLEDIR%\%BUNDLE%.zip CRC32... >> %MAINLOG%
TYPE %CRCDIR%\%BUNDLE%.c32 | TR a-f A-F | PIPESET NEWCRC
CRC32 %BUNDLEDIR%\%BUNDLE%.zip | FAM \F1 | TR a-f A-F | PIPESET OLDCRC
IF "%NEWCRC%" == "%OLDCRC%" GOTO BDCRCOK

ECHO CRC32 Verification faild: %NEWCRC% ne %OLDCRC% >> %MAINLOG%
GOTO END

:BDCRCOK
ECHO Unzip %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%
UNZIP32 -jo %BUNDLEDIR%\%BUNDLE%.zip -d %UNZIPDIR%
IF %ERRORLEVEL% == 0 GOTO UNZIPOK

ECHO Cannot unzip %BUNDLEDIR%\%BUNDLE%.zip to %UNZIPDIR%... >> %MAINLOG%
GOTO END

:UNZIPOK
ECHO Start %GO%... >> %MAINLOG%
CALL %GO%

:END
ECHO Deleting directory %UNZIPDIR% and subdirectories... >> %MAINLOG%
DELDIR %UNZIPDIR%
RD %UNZIPDIR%
timenow >> %MAINLOG%
ECHO ===============================START COMPLETED=============================== >> %MAINLOG%
ECHO.